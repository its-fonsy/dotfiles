# Enable colors and change prompt:
autoload -U colors && colors	# Load colors
setopt autocd                   # Automatically cd into typed directory.
stty stop undef                 # Disable ctrl-s to freeze terminal.

# Prompt
setopt PROMPT_SUBST
autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-revision true

zstyle ':vcs_info:*' formats '(%F{green}%u%c%b%f) '
zstyle ':vcs_info:*' unstagedstr '%f%F{red}'
zstyle ':vcs_info:*' stagedstr '%f%F{yellow}'
precmd () { vcs_info }

PROMPT='$vcs_info_msg_0_%n@%m %3~ $ '

# Remove .xsession-errors
[ -e $HOME/.xsession-errors ] && rm $HOME/.xsession-errors

# GPG stuff
GPG_TTY=$(tty)
export GPG_TTY

# Sources shortcuts and aliases
if [ -f "${XDG_CONFIG_HOME:-$HOME/.config}/aliasrc" ]; then
    source "${XDG_CONFIG_HOME:-$HOME/.config}/aliasrc"
fi


# History in cache directory:
HISTFILE=$XDG_CACHE_HOME/zsh/history
HISTSIZE=10000
SAVEHIST=10000

if [ -f $HISTFILE ]; then
    mkdir -p "${HISTFILE%/*}"
    touch "$HISTFILE"
fi

# zoxide
_ZO_DATA_DIR=$XDG_DATA_HOME/.local/share
_ZO_ECHO=1
_ZO_RESOLVE_SYMLINKS=1
eval "$(zoxide init zsh)"

# History Search
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^R" history-beginning-search-backward-end
bindkey "^T" history-beginning-search-forward-end

# Basic auto/tab complete:
autoload -U compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots)		# Include hidden files.

# vi mode
bindkey -v
export KEYTIMEOUT=1

# Use vim keys in tab complete menu:
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -v '^?' backward-delete-char

# fast edit files
cfg() {
    local cfg_file=$(fzf --reverse --height 20% < $XDG_CONFIG_HOME/configrc)

    if [[ $cfg_file != '' ]]; then
        $EDITOR "$cfg_file"
    fi
}

# Load zsh-syntax-highlighting; should be last.
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null
autoload -Uz compinit && compinit

# Directory Stack copied from
#   https://dev.to/phantas0s/understanding-and-configuring-zsh-3jnd
setopt PUSHD_IGNORE_DUPS    # Do not store duplicates in the stack
setopt PUSHD_SILENT         # Do not print the directory stack after pushd/popd

alias d='dirs -v'
for index ({1..9}) alias "$index"="cd +${index}"; unset index
